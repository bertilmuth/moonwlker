plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven'
}

group = 'org.requirementsascode'
version = '0.0.8'

sourceCompatibility = 8
targetCompatibility = 8

repositories {
	mavenCentral()
}

jar {
    manifest {
        attributes 'Implementation-Title': 'moonwlker',
                   'Implementation-Version': version
	}
}

gradle.projectsEvaluated {
     tasks.withType(JavaCompile) {
         options.compilerArgs << "-parameters"
     }
}

dependencies {
  implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.0'
  testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
}

test {
    useJUnitPlatform()
}

repositories {
	mavenCentral()
	maven {
		url "https://oss.sonatype.org/content/repositories/snapshots/"
	}
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

if (project.hasProperty("signing.keyId")) {
	apply plugin: 'signing'

	signing {
	    sign configurations.archives
	}
	
	uploadArchives {
	    repositories {
	        mavenDeployer {
	            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
	            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
	               authentication(userName: sonatypeUsername, password: sonatypePassword)
	            }
	            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
	               authentication(userName: sonatypeUsername, password: sonatypePassword)
	            } 
	            
	            pom.project {
	               name 'moonwlker'
	               packaging 'jar'
	               description 'JSON without annotation.'
	               url 'http://requirementsascode.org/'
	
	               scm {
	                   url 'scm:git@github.com:bertilmuth/requirementsascode.git'
	                   connection 'scm:git@github.com:bertilmuth/requirementsascode.git'
	                   developerConnection 'scm:git@github.com:bertilmuth/requirementsascode.git'
	               }
	
	               licenses {
	                   license {
	                       name 'The Apache Software License, Version 2.0'
	                       url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
	                       distribution 'repo'
	                   }
	               }
	
	               developers {
	                   developer {
	                       id 'bertilmuth'
	                       name 'Bertil Muth'
	                   }
	               }
	           }
	        }
	    }
	}
}