plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
	id 'maven-publish'
}

group = 'org.requirementsascode'
version = '0.5.1' 

repositories {
	mavenCentral()
	maven {
		url "https://oss.sonatype.org/content/repositories/snapshots/"
	}
}

java {
	toolchain {
	  languageVersion = JavaLanguageVersion.of(11)
	}
    withJavadocJar()
    withSourcesJar()
}

jar {
    manifest {
        attributes 'Implementation-Title': 'moonwlker',
                   'Implementation-Version': archiveVersion
	}
}

gradle.projectsEvaluated {
     tasks.withType(JavaCompile) {
         options.compilerArgs << "-parameters"
     }
}

dependencies {
  implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
  testImplementation("org.junit.jupiter:junit-jupiter-api:5.10.3")
  testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.10.3")
}

test {
    useJUnitPlatform()
}

if (project.hasProperty("signing.keyId")) {
	apply plugin: 'signing'

	signing {
	    sign configurations.archives
	}

	publishing {
		publications {
			mavenJava(MavenPublication) {
				pom{
					name = 'moonwlker'
					description = 'JSON without annotation.'
					url = 'http://requirementsascode.org/'
					packaging = 'jar'

					from components.java

					licenses {
						license {
							name = 'The Apache Software License, Version 2.0'
							url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						}
					}

					developers {
						developer {
							id = 'bertilmuth'
							name = 'Bertil Muth'
						}
					}

					scm {
						url = 'scm:git@github.com:bertilmuth/requirementsascode.git'
						connection = 'scm:git@github.com:bertilmuth/requirementsascode.git'
						developerConnection = 'scm:git@github.com:bertilmuth/requirementsascode.git'
					}
				}
			}
		}

		repositories {
			maven {
				credentials {
					username sonatypeUsername
					password sonatypePassword
				}

				def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
				def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
				url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
			}
		}
	}

	signing {
		sign publishing.publications.mavenJava
	}
}